plugins {
    id 'maven'
    id 'idea'
    id 'war'
    id "se.inera.intyg.plugin.common" version "1.0.62"
    id "org.akhikhl.gretty" version "1.4.2"
}

ext {
    commonVersion = System.properties['commonVersion'] ?: '0-SNAPSHOT'
    infraVersion = System.properties['infraVersion'] ?: '0-SNAPSHOT'

    commonsIoVersion = "2.4"
    funktionstjansterGrpSchemasVersion = "1.0.3"
    insurancemedicinedecisionsupportSchemasVersion = "1.0.1"
    intygClinicalprocessSchemasVersion = "1.0.7-beta3"
    intygInsuranceprocessSchemasVersion = "1.0.4"
    intygTransportstyrelsenSchemasVersion = "1.0.4"
    ppSchemasVersion = "1.0.4"
    ppTermsSchemasVersion = "1.0.4"
    rivtaClinicalprocessSchemasVersion = "3.2.0-RC2"
    rivtaInsuranceprocessSchemasVersion = "3.1.0.4"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.3"
    rivtaItintegrationSchemasVersion = "1.0.0.4"
    rivtaResidentMasterSchemasVersion = "1.1.3.5"

    activeMqVersion = "5.13.4"
    awaitilityVersion = "1.4.0"
    camelVersion = "2.16.0"
    geronimoJmsVersion = "1.1.1"
    javaxWsRsApiVersion = "2.0.1"
    servletApiVersion = "2.5"
    springVersion = "4.3.16.RELEASE"
    xmlUnitVersion = "1.4"

    mockitoVersion = "1.9.0"
    springockitoVersion = "1.0.8"
    mysqlVersion = "5.1.40"
    h2Version = "1.3.176"
}

group = 'se.inera.intyg.logsender'
version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

task createVersionPropertyFile(type: VersionPropertyFileTask)

uploadArchives.repositories.mavenDeployer {
    repository(url: "https://build-inera.nordicmedtest.se/nexus/repository/releases/") {
        authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
    }
}

gretty {
    contextPath = '/logsender'
    contextConfigFile = file("src/main/webapp/WEB-INF/jetty-web.xml")
    integrationTestTask = null
    scanInterval = 0
    httpPort = 9099
    debugPort = 5009
    debugSuspend = false

    jvmArgs = [
            "-Dcatalina.base=${buildDir}/catalina.base",
            "-Dspring.profiles.active=dev",
            "-Dcredentials.file=${projectDir}/log-sender-dummy-credentials.properties",
            "-Dconfig.file=${projectDir}/log-sender-dev.properties",
            "-Djava.awt.headless=true"
    ]

    extraResourceBase 'build/apps/'
}

test {
    exclude '**/*IT*'
}

tasks.withType(War) {
    destinationDir = file("$rootDir/web/build/libs")
}

task camelTest(type: Test) {
    outputs.upToDateWhen { false }
    include '**/*IT*'
}

task tagRelease(type: TagReleaseTask)

dependencies {
    compile "se.inera.intyg.infra:log-messages:${infraVersion}"
    compile "se.inera.intyg.infra:loggtjanst-stub:${infraVersion}"
    compile "se.inera.intyg.common:logging-util:${commonVersion}"
    compile "se.inera.intyg.common:integration-util:${commonVersion}"
    compile "se.inera.intyg.infra:monitoring:${infraVersion}"

    compile "org.springframework:spring-web:${springVersion}"
    compile "org.springframework:spring-jms:${springVersion}"
    compile "org.apache.activemq:activemq-spring:${activeMqVersion}"
    compile "org.apache.camel:camel-spring:${camelVersion}"
    compile "org.apache.activemq:activemq-camel:${activeMqVersion}"
    compile "javax.ws.rs:javax.ws.rs-api:${javaxWsRsApiVersion}"

    compileOnly "javax.servlet:servlet-api:${servletApiVersion}"
    compileOnly "org.apache.geronimo.specs:geronimo-jms_1.1_spec:${geronimoJmsVersion}"

    testCompile 'junit:junit:4.+'
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.kubek2k:springockito:${springockitoVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "xmlunit:xmlunit:${xmlUnitVersion}"
    testCompile "com.jayway.awaitility:awaitility:${awaitilityVersion}"
    testCompile "org.apache.camel:camel-test-spring:${camelVersion}"
    testCompile "org.apache.activemq:activemq-broker:${activeMqVersion}"

    gretty "org.apache.activemq:activemq-client:${activeMqVersion}"
    gretty "org.apache.activemq:activemq-broker:${activeMqVersion}"
    gretty "com.h2database:h2:${h2Version}"
    gretty "mysql:mysql-connector-java:${mysqlVersion}"
}

repositories {
    mavenLocal()
    maven { url "https://build-inera.nordicmedtest.se/nexus/repository/releases/" }
    maven { url "http://repo.maven.apache.org/maven2" }
}
