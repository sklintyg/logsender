plugins {
    id 'maven'
    id 'idea'
    id 'war'
    id "se.inera.intyg.plugin.common" version "3.1.3"
    id "org.gretty" version "3.0.1"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
}

ext {
    commonVersion = System.properties['commonVersion'] ?: '3.15.0-SNAPSHOT'
    infraVersion = System.properties['infraVersion'] ?: '3.15.0-SNAPSHOT'

    rivtaStoreLogSchemasVersion = "2.0.1"

    apacheCXFVersion = "3.2.4"
    camelVersion = "3.2.0"
    geronimoVersion = "1.1.1"

    springbootMavenBomVersion = "2.2.5.RELEASE"
}

group = 'se.inera.intyg.logsender'
version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

task createVersionPropertyFile(type: VersionPropertyFileTask)

uploadArchives.repositories.mavenDeployer {
    repository(url: "https://nexus.drift.inera.se/repository/maven-releases/") {
        authentication(userName: System.properties['ineraNexusUsername'], password: System.properties['ineraNexusPassword'])
    }
}

gretty {
    servletContainer = 'tomcat9'
    contextPath = '/logsender'

    integrationTestTask = null
    scanInterval = 0
    httpPort = 9099
    debugPort = 5009
    debugSuspend = false

    jvmArgs = [
            "-Dcatalina.base=${buildDir}/catalina.base",
            "-Dspring.profiles.active=dev,wc-loggtjanst-stub,testability-api,caching-enabled",
            "-Dprojectdir=${projectDir}",
            "-Dcredentials.file=${projectDir}/log-sender-dummy-credentials.properties",
            "-Dconfig.file=${projectDir}/log-sender-dev.properties",
            "-Dlogback.file=classpath:logback-dev.xml",
            "-Djava.awt.headless=true"
    ]

    extraResourceBase 'build/apps/'
}

test {
    useJUnitPlatform()
    exclude '**/*IT*'
    jvmArgs = gretty.jvmArgs
}

tasks.withType(War) {
    destinationDirectory = file("$rootDir/web/build/libs")
}

task camelTest(type: Test) {
    useJUnitPlatform()
    outputs.upToDateWhen { false }
    include '**/*IT*'
    jvmArgs = gretty.jvmArgs
}

task tagRelease(type: TagReleaseTask)

war.dependsOn createVersionPropertyFile

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
    }
}

dependencies {
    compile "se.riv.informationsecurity.auditing.log:informationsecurity-auditing-log-schemas:${rivtaStoreLogSchemasVersion}"

    compile "se.inera.intyg.infra:log-messages:${infraVersion}"
    compile "se.inera.intyg.infra:loggtjanst-stub:${infraVersion}"
    compile "se.inera.intyg.infra:monitoring:${infraVersion}"
    compile "se.inera.intyg.common:logging-util:${commonVersion}"
    compile "se.inera.intyg.common:integration-util:${commonVersion}"

    compile "com.sun.activation:jakarta.activation"
    compile "jakarta.ws.rs:jakarta.ws.rs-api"
    compile "org.glassfish.jaxb:jaxb-runtime"

    compile "org.springframework:spring-jms"
    compile "org.springframework:spring-web"
    compile "org.springframework:spring-webmvc"

    compile "org.apache.activemq:activemq-spring"
    compile "org.apache.camel:camel-activemq:${camelVersion}"
    compile "org.apache.camel:camel-spring:${camelVersion}"
    compile "org.apache.camel:camel-jms:${camelVersion}"
    compile "org.apache.camel:camel-spring-javaconfig:${camelVersion}"
    compile "org.apache.camel:camel-direct:${camelVersion}"

    compile "org.apache.cxf:cxf-rt-frontend-jaxws:${apacheCXFVersion}"
    compile "org.apache.cxf:cxf-rt-transports-http:${apacheCXFVersion}"
    compile "org.apache.cxf:cxf-rt-features-logging:${apacheCXFVersion}"

    compileOnly "jakarta.servlet:jakarta.servlet-api"
    compileOnly "org.apache.geronimo.specs:geronimo-jms_1.1_spec:${geronimoVersion}"

    testCompile "org.apache.activemq:activemq-broker"
    testCompile "org.apache.camel:camel-test-spring-junit5:${camelVersion}"
    testCompile "org.awaitility:awaitility"
    testCompile "org.junit.jupiter:junit-jupiter-api"
    testCompile "org.mockito:mockito-junit-jupiter"
    testCompile "org.springframework:spring-test"
    testCompile "org.xmlunit:xmlunit-legacy"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    gretty "org.apache.activemq:activemq-client"
    gretty "org.apache.activemq:activemq-broker"
    gretty "com.h2database:h2"
    gretty "mysql:mysql-connector-java"
}

repositories {
    mavenLocal()
    maven { url "https://nexus.drift.inera.se/repository/it-public/" }
    jcenter {
        content {
            // this repository contains everything BUT artifacts with group starting with "se.inera"
            excludeGroupByRegex "se\\.inera.*"
        }
    }
}
